# -*- mode: Snakemake -*-
# Processing Rules

rule all_uniq_sites:
  input:
    expand(RUN_DIR + "/output/uniqSites/{sample}.uniq.csv", sample=SAMPLES)
  output:
    RUN_DIR + "/output/unique_sites." + RUN + ".csv"
  params:
    RUN_DIR + "/output"
  shell:
    """
    head -n 1 -q {params}/uniqSites/* | uniq > {output}
    cat {params}/uniqSites/* | sed '/seqnames/d' >> {output}
    """

rule all_edit_sites:
  input:
    sites = RUN_DIR + "/output/unique_sites." + RUN + ".csv",
    umitag = all_edit_sites_inputs
  output:
    RUN_DIR + "/output/edited_sites." + RUN + ".rds"
  params:
    config = RUN_DIR + "/" + "config.yml",
    tool = ROOT_DIR + "/tools/processing/post_process.R"
  log:
    RUN_DIR + "/logs/" + RUN + ".postproc.log"
  run:
    if (config["UMItags"]):
      shell("Rscript {params.tool} {input.sites} -o {output} " + \
        "-c {params.config} -u {input.umitag} > {log} 2>&1")
    else:
      shell("Rscript {params.tool} {input.sites} -o {output} " + \
        "-c {params.config} > {log} 2>&1")

def report_supp(wildcards):
  supp_str = str()
  if (config["figures"]): 
      supp_str = supp_str + "-f "
  if (config["reportData"]):
      supp_str = supp_str + "-d "
  if (config["suppFile"]):
      supp_str = supp_str + "-s " + ROOT_DIR + "/" + config["Supplemental_Info"]
  return supp_str
        
rule run_report:
  input: RUN_DIR + "/output/edited_sites." + RUN + ".rds"
  output: RUN_DIR + "/reports/report." + RUN + ".html"
  params: 
    tool = ROOT_DIR + "/tools/processing/generate_iGUIDE_report.R",
    config = RUN_DIR + "/" + "config.yml",
    supp = report_supp
  log: RUN_DIR + "/logs/" + RUN + ".report.log"
  shell:
    """
    Rscript {params.tool} {input} -o {output} \
      -c {params.config} {params.supp} > {log} 2>&1
    """
